#
# Theme based on steeef
# and mitsuhikos prompt: https://github.com/mitsuhiko/dotfiles
#

function prompt_andihit_precmd {
  if [[ ! "$PROMPT_ENABLE_VCSINFO" = "n" ]]; then
    # Check for untracked files or updated submodules since vcs_info does not.
    if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
      branch_format="(${_prompt_andihit_colors[1]}%b%f%u%c${_prompt_andihit_colors[4]}●%f)"
    else
      branch_format="(${_prompt_andihit_colors[1]}%b%f%u%c)"
    fi

    zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

    vcs_info 'prompt'
  fi

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_andihit_setvcsinfo {
  autoload -Uz vcs_info

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_andihit_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_andihit_colors[5]}%a%f)"
  local unstaged_format="${_prompt_andihit_colors[2]}●%f"
  local staged_format="${_prompt_andihit_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""
}

function prompt_andihit_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_andihit_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_andihit_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_andihit_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # use different color for root
  if [[ "$USER" == "root" ]]; then
    local user_color=%F{red}
  else
    local user_color=%F{magenta}
  fi

  if [[ ! "$PROMPT_ENABLE_VCSINFO" = "n" ]]; then
    prompt_andihit_setvcsinfo
  fi

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="
%B${user_color}%n%f at %F{yellow}%m%f in %F{green}%~%f %(?..exited %F{red}%?%f )"'${vcs_info_msg_0_}'"
"'$python_info[virtualenv]'"$ %b"
  RPROMPT="%B[%*]%b"
}

prompt_andihit_setup "$@"
