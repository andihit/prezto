#
# Theme based on steeef
# and mitsuhikos prompt: https://github.com/mitsuhiko/dotfiles
#

ANDIHIT_BASEPROMPT='%B%(#.%F{red}.%F{magenta})%n%f at %F{yellow}%m%f in %F{green}%~%f'

function prompt_andihit_worker_callback {
  output=$3

  PROMPT=${output}
  zle && zle .reset-prompt
}

function prompt_andihit_precmd_async {
  builtin cd -q "$*"

  # %b = branch
  local branch_format="on %F{blue}%s%f:%b"
  local action_format="(%F{green}%a%f)"

  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    local untracked_files="%F{green}?"
  fi

  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "%F{green}+"
  zstyle ':vcs_info:*:prompt:*' stagedstr "%F{cyan}+"
  zstyle ':vcs_info:*:prompt:*' actionformats " ${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats " ${branch_format}%c%u${untracked_files}%f"
  zstyle ':vcs_info:*:prompt:*' nvcsformats ""
  vcs_info 'prompt'

  zstyle ':prezto:module:python:info:virtualenv' format ' (%v)'
  if (( $+functions[python-info] )); then
    # TODO: doesn't work because worker process doesn't have env variable set
    python-info
  fi

  local last_exit_code="%(?.. exited %F{red}%?%f)"

  echo $'\n'${ANDIHIT_BASEPROMPT}${vcs_info_msg_0_}$python_info[virtualenv]${last_exit_code}$' \n$%b '
}

function prompt_andihit_precmd {
  async_flush_jobs prompt_andihit_worker
  async_job prompt_andihit_worker prompt_andihit_precmd_async ${PWD}
}

function prompt_andihit_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook vcs_info
  autoload -Uz async && async

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_andihit_precmd
  async_start_worker prompt_andihit_worker -n
  async_register_callback prompt_andihit_worker prompt_andihit_worker_callback

  # Define prompts.
  PROMPT=$'\n'${ANDIHIT_BASEPROMPT}$' \n$%b '
  RPROMPT="%B[%*]%b"
}

prompt_andihit_setup "$@"
